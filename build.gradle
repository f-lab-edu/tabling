plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.3'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'jacoco'
    id "org.asciidoctor.jvm.convert" version '3.3.2' // asciidoctor 플러그인 추가
}

configurations {
    asciidoctorExtensions // asciidoctorExt에 대한 선언
}

group = 'com.flab'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

jacoco {
    toolVersion = "0.8.9"
    reportsDirectory = layout.buildDirectory.dir("reports/jacoco")
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    asciidoctorExt // rest-docs
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.security:spring-security-crypto'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.h2database:h2'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    // easy-random: 랜덤 테스트 데이터 생성 라이브러리
    testImplementation 'org.jeasy:easy-random-core:5.0.0'
    //rest-docs
    asciidoctorExtensions 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
}

ext {
    snippetsDir = file('$buildDir/generated-snippets') // 스니펫이 생성되는 디렉터리 경로를 설정
}

tasks.named('test') {
    systemProperty("org.springframework.restdocs.outputDir", snippetsDir)
    outputs.dir snippetsDir // 스니펫이 생성되는 디렉터리를 설정
    finalizedBy 'asciidoctor'
}

asciidoctor { // Gradle이 asciidoctor Task를 수행하는 설정 (함수 선언)
    dependsOn test // Gradle의 test Task 이후 asciidoctor를 수행
    configurations 'asciidoctorExtensions' // asciidoctor 확장 설정
    baseDirFollowsSourceFile() // .adoc 파일을 include 하면서 사용하기 위한 설정
    inputs.dir snippetsDir // 스니펫을 불러올 위치 설정
}

asciidoctor.doFirst { // asciidoctor Task가 수행될 때 가장 먼저 수행
    delete file('src/main/resources/static/docs')
}

task copyDocument(type: Copy) { // 생성된 html 파일을 옮긴다
    dependsOn asciidoctor // Gradle의 asciidoctor Task 이후 수행
    from file("${asciidoctor.outputDir}")
    into file("src/main/resources/static/docs")
}


build {
    dependsOn copyDocument
}

bootJar {
    dependsOn asciidoctor // asciidoctor 이후 bootJar 수행
    from ("${asciidoctor.outputDir}") {
        into 'static/docs'
    }
}

tasks.register('testCoverage', Test) {
    group 'verification'
    description 'Runs the unit tests with coverage'

    dependsOn(':test',
            ':jacocoTestReport',
            ':jacocoTestCoverageVerification')

    tasks['jacocoTestReport'].mustRunAfter(tasks['test'])
    tasks['jacocoTestCoverageVerification'].mustRunAfter(tasks['jacocoTestReport'])
}

jacocoTestReport {
    reports {
        xml.required = true
        csv.required = false
        html.required = true
    }
}

jacocoTestCoverageVerification {
    violationRules {
        /*
        클래스 단위로 브랜치와 라인 커버리지 비율을 체크한다.
        Lombok으로 생성된 코드는 @Generated를 통해 테스트 커버리지 대상에서 제외된다.
         */
        rule {
            element = 'CLASS'

            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }

            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }

            excludes = [
                    'com.flab.tabling.TablingApplication',
                    'com.flab.tabling.**.config.**',
                    'com.flab.tabling.**.exception.**',
                    'com.flab.tabling.**.dto.**',
                    'com.flab.tabling.global.audit.**',
                    'com.flab.tabling.global.env.**',
                    'com.flab.tabling.global.constant.**',
                    'com.flab.tabling.global.auth.ExceptionHandlerFilter',
                    'com.flab.tabling.global.auth.Login*'
            ]
        }
    }
}

